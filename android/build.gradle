group = "com.example.filament_widget"
version = "1.0-SNAPSHOT"

buildscript {
    ext.kotlin_version = "2.1.0"
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath("com.android.tools.build:gradle:8.7.3")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version")
    }
}

apply plugin: "com.android.library"
apply plugin: "kotlin-android"

repositories {
    google()
    mavenCentral()

    if (project.hasProperty("filamentWidgetUseLocalMaven")) {
        def localMaven = file("$rootDir/../tooling/filament-android-maven")
        if (localMaven.exists()) {
            def lfsPointerFound = fileTree(localMaven).any { file ->
                file.isFile() && file.length() < 1024 && file.text.contains("git-lfs.github.com/spec/v1")
            }
            if (lfsPointerFound) {
                logger.lifecycle("filament_widget: local Maven mirror contains Git LFS pointers; skipping.")
            } else {
                maven {
                    url = uri(localMaven)
                }
            }
        }
    }
}

android {
    namespace = "com.example.filament_widget"

    compileSdk = 35

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11
    }

    sourceSets {
        main.java.srcDirs += "src/main/kotlin"
        test.java.srcDirs += "src/test/kotlin"
    }

    defaultConfig {
        minSdk = 21
    }

    buildTypes {
        debug {
            ndk {
                abiFilters "arm64-v8a", "x86_64"
            }
        }
        release {
            ndk {
                abiFilters "arm64-v8a"
            }
        }
    }

    dependencies {
        implementation "com.google.android.filament:filament-android:1.68.4"
        implementation "com.google.android.filament:gltfio-android:1.68.4"
        implementation "com.google.android.filament:filament-utils-android:1.68.4"
        implementation "com.google.android.filament:filamat-android:1.68.4"
        testImplementation("org.jetbrains.kotlin:kotlin-test")
        testImplementation("org.mockito:mockito-core:5.0.0")
    }

    testOptions {
        unitTests.all {
            useJUnitPlatform()

            testLogging {
               events "passed", "skipped", "failed", "standardOut", "standardError"
               outputs.upToDateWhen {false}
               showStandardStreams = true
            }
        }
    }
}
